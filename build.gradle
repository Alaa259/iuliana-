allprojects {
    version = '1.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8
    // Make sure transitive project dependencies are resolved
    configurations.compile.transitive = true

    repositories {
        mavenCentral()
        jcenter()
    }

    configurations {
        all*.exclude group: "commons-logging", module: "commons-logging"
    }
}

//We define versions for the libraries we will be using
ext {
    springVersion = '4.1.5.RELEASE'
    springJpaVersion = '1.7.1.RELEASE'
    springSecurityVersion = '3.2.5.RELEASE'
    webflowVersion = '2.4.1.RELEASE'
    junitVersion = '4.12'
    slf4jVersion = '1.7.9'
    tomcatVersion = '8.0.15'
    aspectjVersion = '1.8.4'
    jodaVersion = '2.6'
    tilesVersion = '3.0.5'
    hibernateVersion = '4.3.7.Final'
    h2Version = '1.4.184'
    logbackVersion = '1.1.2'

    //we group libraries by their purpose so we can import them easier in the modules
    misc = [
            h2         : "com.h2database:h2:$h2Version",
            slf4jApi   : "org.slf4j:slf4j-api:$slf4jVersion",
            slf4jJcl   : "org.slf4j:jcl-over-slf4j:$slf4jVersion",
            logback    : "ch.qos.logback:logback-classic:$logbackVersion",
            joda       : "joda-time:joda-time:$jodaVersion",
            junit      : "junit:junit:$junitVersion",
            dbcp       : "org.apache.openejb:commons-dbcp-all:1.3",
            jstl       : "javax.servlet:jstl:1.2",
            tilesJsp   : "org.apache.tiles:tiles-jsp:$tilesVersion",
            tilesReqApi: "org.apache.tiles:tiles-request-api:1.0.6"
    ]

    hibernate = [
            validator: "org.hibernate:hibernate-validator:5.1.3.Final",
            ehcache  : "org.hibernate:hibernate-ehcache:$hibernateVersion",
            em       : "org.hibernate:hibernate-entitymanager:$hibernateVersion",
            core     : "org.hibernate:hibernate-core:$hibernateVersion",
    ]


    spring = [
            data           : "org.springframework.data:spring-data-jpa:$springJpaVersion",
            test           : "org.springframework:spring-test:$springVersion",
            webmvc         : "org.springframework:spring-webmvc:$springVersion",
            webflow        : "org.springframework.webflow:spring-webflow:$webflowVersion",
            aop            : "org.springframework:spring-aop:$springVersion",
            jdbc           : "org.springframework:spring-jdbc:$springVersion",
            orm            : "org.springframework:spring-orm:$springVersion",
            contextSupport : "org.springframework:spring-context-support:$springVersion",
            securityWeb    : "org.springframework.security:spring-security-web:$springSecurityVersion",
            securityTaglibs: "org.springframework.security:spring-security-taglibs:$springSecurityVersion",
            securityConfig : "org.springframework.security:spring-security-config:$springSecurityVersion"
    ]
}

task allDeps <<{
    description 'Task used to display dependencies for all modules in the project'
    subprojects.each { p ->
        println()
        println " $p.name ".center(60, '*')
        println()
        p.configurations.all.findAll { !it.allDependencies.empty }.each { c ->
            println " ${c.name} ".center(60, '-')
            c.allDependencies.each { dep ->
                println "$dep.group:$dep.name:$dep.version"
            }
            println "-" * 60
        }
    }
}


task allCompile << {
    description 'Task used to compile all modules in the project without running tests'
    subprojects.each{ p ->
        println "Building without tests $p.name ".center(50, '.')
        p.getTasksByName("compileJava", true).each {t ->
            t.execute();
        }
    }
}
